/*
 * generated by Xtext
 * 
 */
package de.tu_bs.cs.isf.mbse.mbtimes.validation

import de.tu_bs.cs.isf.mbse.mbtimes.npl.Date
import de.tu_bs.cs.isf.mbse.mbtimes.npl.Declaration
import de.tu_bs.cs.isf.mbse.mbtimes.npl.NplPackage
import java.util.Arrays
import org.eclipse.xtext.validation.Check

/**
 * This class contains constraints for the defined DSL. 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class NplValidator extends AbstractNplValidator {

	/**
	 * This constraint checks whether the newspaper title contains less than 21 characters.
	 */
	@Check
	def checkNewspaperDeclarationLength(Declaration declaration) {
		if (declaration.title.length > 20) {
			error("A newspaper title must not be longer than 20 characters.", 
				NplPackage.Literals.DECLARATION__TITLE
			)
		}
	}
	
	/**
	 * This constraint checks whether at least one topic has been defined.
	 */
	 @Check
	 def checkNumberOfTopics(Declaration declaration) {
	 	if (declaration.topics.empty) {
	 		error("Please specify at least one topic!", NplPackage.Literals.DECLARATION__TOPICS)
	 	} 
	 } 
	

	/**
	 * This constraint checks whether the date specified by day, month and year is valid.
	 */
	@Check
	def checkDate(Date date) {
		var int[] days30 = #[4,6,9,11];
		if (date.month < 1 || date.month > 12) {
			error("Number " + date.month + " does not refer to any valid month.", NplPackage.Literals.DATE__MONTH)
		} 
		if (date.day < 1) {
			error("Please enter a positive number.", NplPackage.Literals.DATE__DAY)
		}
		if (date.month.equals(2)) {
			if (date.year % 4 == 0 && date.day > 29) {
				error("In a leap-year, february has 29 days.", NplPackage.Literals.DATE__DAY)
			} else if (date.day > 28) {
				error("Usually, february has 28 days.", NplPackage.Literals.DATE__DAY)
			}
		} else {
			if (Arrays.asList(days30).contains(date.month) && (date.day > 30)) {
				error("This month has only 30 days.", NplPackage.Literals.DATE__DAY)
			} else if (date.day > 31) {
				error("This month has only 31 days.", NplPackage.Literals.DATE__DAY)
			}
			
			if (date.month == 12 && date.day < 27 && date.day > 23) {
				warning("Merry Christmas! :)", NplPackage.Literals.DATE__DAY)
			} 
		}
	}
	
	/**
	 * This constraint checks whether the number of articles is positive and smaller than 6.
	 */
	@Check
	def checkNrArticles(Declaration declaration) {
		if (declaration.articleCnt < 1) {
			error("Number of articles must be greater than 0", NplPackage.Literals.DECLARATION__ARTICLE_CNT)
		}
	}
	 
	 /**
	  * This constraint checks whether the inserted number of words in an article is positive and if maximum number
	  * of words is greater than minimum number of words.
	  */
	 @Check
	 def checkNumberOfWords(Declaration declaration) {
	 	if (declaration.articleWordsMin < 1) {
	 		error("The minimum number of words could not be smaller than 1", NplPackage.Literals.DECLARATION__ARTICLE_WORDS_MIN)
	 	}
	 	if (declaration.articleWordsMin > declaration.articleWordsMax) {
	 		error("The minimum number of words could not be smaller than the maximum number of words", NplPackage.Literals.DECLARATION__ARTICLE_WORDS_MAX)
	 	}
	 }
	 
	 /**
	 * This constraint checks whether the number of images is positive and smaller than 6.
	 */
	@Check
	def checkNrImages(Declaration declaration) {
		if (declaration.imagesCnt.value < 1) {
			error("Number of images must be greater than 0", NplPackage.Literals.DECLARATION__IMAGES_CNT)
		}
	}
	 
	/**
	 * This constraint checks whether the number of columns is positive and smaller than 6. 
	 * Else, LaTeX generates an error.
	 */
	 @Check
	 def checkNumberOfColumns(Declaration declaration) {
	 	if (declaration.columnsCnt < 1 || declaration.columnsCnt > 5) {
	 		error("Number of columns must be greater than 0 and smaller than 6", 
	 			NplPackage.Literals.DECLARATION__COLUMNS_CNT
	 		)
	 	}
	 	if (declaration.columnsCnt > 3) {
	 		warning("You'll have a big newspaper, huh?", 
	 			NplPackage.Literals.DECLARATION__COLUMNS_CNT
	 		)
	 	}
	 }
	
	/**
	 * This constraint checks whether the price has a positive value. If more than two decimal
	 * places are specified, the number of decimal places will be reduced to two.
	 */ 
	 @Check
	 def checkPrice(Declaration declaration) {
	 	if (declaration.price.value < 0) { // terminal rule for INT already covers values below 0 as error
	 		error("You should pay for the newspaper, not us ;)", NplPackage.Literals.PRICE__VALUE);
	 	}
	 	if (Float.toString(declaration.price.value).split("\\.").get(1).length() > 2) {
	 		warning("We only consider two decimal places.", NplPackage.Literals.PRICE__VALUE);
	 	}
	 }

}
