
//this line indicates we want the new grammar to be derived from the build in grammar terminals
// which defines some basic terminal rules like STRING, ID and so forth
grammar de.tu_bs.cs.isf.mbse.mbtimes.Npl with org.eclipse.xtext.common.Terminals
//grammar de.tu_bs.cs.isf.mbse.mbtimes.Npl
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

//this line defines the name and namespace URI for our grammar
generate npl "http://www.tu_bs.de/cs/isf/mbse/mbtimes/Npl"

/*
 * Definition of the newspaper grammar. Among other things, Xtext generates a parser, Ecore model and an editor
 * based on the grammar. 
 */
Declaration:
	"newspaper" name=ID "{" &
	"title" title = STRING
	"feed links" "{"
		feedlinks += Pair (feedlinks+=Pair)*
	"}" &
	"topics" "{" 
		topics +=Topic (topics+=Topic)*
	"}" &
	("date" date=Date)? &
	("location" location=STRING)? &
	("price" price=Price)? &
	("volume" volume=INT ("no." number=INT)? )? &
	"language" language=Language &
	"format" format=Format &
	"number of articles per topic" articleCnt=INT &
	"number of words per article" articleWordsMin=INT "-" articleWordsMax=INT &
	("number of images per article" imagesCnt=ImagesCount)? &
	"number of columns" columnsCnt=INT &
	"font size" fontSize=FontSize &
"}";

Pair:
	type=FeedType key = STRING "<" value = URL ">"
;

FeedType:
	"RSS" |
	"Atom"
;

Topic:
	"topic" name=ID "as" title = STRING  "{"
	tags+=STRING (", " tags+=STRING)*
	"}";
	
Date: 
	"{" day=INT '-' month=INT '-' year=INT "}";
	
Price:
	"{" value=Float currency=Currency "}";
	
Currency:
	value = "Euro" |
	value = "EUR" |
	value = "â‚¬" |
	value = "Dollar" |
	value = "USD" |
	value = "$";	
	
Float returns ecore::EFloat: INT (('.'|',') INT)?;

Language:
	 value = "English" | 
	 value = "German";
	 
Format:
	value =  "DIN A4" | 
	value =  "DIN A3" | 
	value = "DIN A2" | 
	value = "DIN A1" |
	value = "DIN A0";

ImagesCount:
	value = INT
;

FontSize:
	value =  "small"  | 
	value =  "medium" | 
	value =  "large";

// URLS are assumed to be of the form "protocol://host[:port][path][query]"
terminal URL:
    ('a'..'z')+ '://' // Protocol
 
    ( // Hostname
        (
            ('0'..'9')+ '.' ('0'..'9')+ '.' ('0'..'9')+ '.' ('0'..'9')+
        ) |
        (
            ('a'..'z'|'A'..'Z')
            (
                'a'..'z'|'A'..'Z'|
                '0'..'9'|
                '$'|
                '-'|
                '_'|
                (
                    '\\'
                    ('0'..'9'|'a'..'f'|'A'..'F')
                    ('0'..'9'|'a'..'f'|'A'..'F')
                ) |
                (
                    '%'
                    ('0'..'9')
                    ('0'..'9')
                )
            )*
            (
                '.'
                ('a'..'z'|'A'..'Z')
                (
                    'a'..'z'|'A'..'Z'|
                    '0'..'9'|
                    '$'|
                    '-'|
                    '_'|
                    (
                        '\\'
                        ('0'..'9'|'a'..'f'|'A'..'F')
                        ('0'..'9'|'a'..'f'|'A'..'F')
                    ) |
                    (
                        '%'
                        ('0'..'9')
                        ('0'..'9')
                    )
                )*
            )*
        )
    )
    (':' ('0'..'9')+)? // Portnumber
 
    ('/' // Path
        (
            'a'..'z'|'A'..'Z'|
            '0'..'9'|
            '$'|
            '-'|
            '_'|
            '~'|
            '+'|
            '.'|
            (
                '\\'
                ('0'..'9'|'a'..'f'|'A'..'F')
                ('0'..'9'|'a'..'f'|'A'..'F')
            ) |
            (
                '%'
                ('0'..'9')
                ('0'..'9')
            )
        )*
    )* 
 
    ('?' // query
        (
            'a'..'z'|'A'..'Z'|
            '0'..'9'|
            '$'|
            '-'|
            '_'|
            '='|
            '&'|
            ';'|
            (
                '\\'
                ('0'..'9'|'a'..'f'|'A'..'F')
                ('0'..'9'|'a'..'f'|'A'..'F')
            ) |
            (
                '%'
                ('0'..'9')
                ('0'..'9')
            )
        )+
 
        ('+'
            (
                'a'..'z'|'A'..'Z'|
                '0'..'9'|
                '$'|
                '-'|
                '_'|
                '='|
                '&'|
                ';'|
                (
                    '\\'
                    ('0'..'9'|'a'..'f'|'A'..'F')
                    ('0'..'9'|'a'..'f'|'A'..'F')
                ) |
                (
                    '%'
                    ('0'..'9')
                    ('0'..'9')
                )
            )+
        )*
    )?; 
